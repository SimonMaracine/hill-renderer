#version 430 core

in vec3 v_fragment_normal;
in vec3 v_fragment_position;

layout(location = 0) out vec4 o_color;

uniform vec3 u_view_position;

struct MaterialBasic {
    vec3 ambient_color;
    vec3 diffuse_color;
    vec3 specular_color;
    float shininess;
};

uniform MaterialBasic u_material;

struct DirectionalLight {
    vec3 direction;
    vec3 ambient_color;
    vec3 diffuse_color;
    vec3 specular_color;
};

uniform DirectionalLight u_directional_light;

vec3 phong(DirectionalLight directional_light, MaterialBasic material, vec3 fragment_normal, vec3 fragment_position, vec3 view_position) {
    fragment_normal = normalize(fragment_normal);
    const vec3 light_direction = normalize(-directional_light.direction);
    const vec3 view_direction = normalize(view_position - fragment_position);
    const vec3 reflection_direction = reflect(-light_direction, fragment_normal);

    // Ambient
    const vec3 ambient_light = directional_light.ambient_color * material.ambient_color;

    // Diffuse
    const float diffuse_strength = max(dot(fragment_normal, light_direction), 0.0);
    const vec3 diffuse_light = diffuse_strength * directional_light.diffuse_color * material.diffuse_color;

    // Specular
    const float specular_strength = pow(max(dot(view_direction, reflection_direction), 0.0), material.shininess);
    const vec3 specular_light = specular_strength * directional_light.specular_color * material.specular_color;

    return ambient_light + diffuse_light + specular_light;
}

void main() {
    const vec3 color = phong(u_directional_light, u_material, v_fragment_normal, v_fragment_position, u_view_position);
    o_color = vec4(color, 1.0);
}
